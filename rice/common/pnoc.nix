# TODO upstream this to Aquaris

{ pkgs, lib, config, aquaris, ... }@top:
let
  inherit (lib)
    flip
    hasPrefix
    mapAttrs'
    mkBefore
    mkIf
    mkOption
    pipe
    splitString
    zipAttrs
    ;

  inherit (lib.types)
    attrsOf
    coercedTo
    listOf
    nullOr
    path
    str
    submodule
    ;

  inherit (config.aquaris) secret;

  join = builtins.concatStringsSep " ";

  empty = pkgs.dockerTools.buildImage {
    name = "empty";
    tag = "latest";
  };

  container = { name, config, ... }: {
    options = {
      cmd = mkOption { type = listOf str; };

      environment = mkOption {
        type = attrsOf str;
        default = { };
      };

      environmentFiles = mkOption {
        type = listOf path;
        default = [ ];
      };

      extraOptions = mkOption {
        type = listOf str;
        default = [ ];
      };

      ports = mkOption {
        type = listOf str;
        default = [ ];
      };

      volumes = mkOption {
        type = listOf str;
        default = [ ];
      };

      workdir = mkOption {
        type = nullOr str;
        default = null;
      };

      ##### special args #####

      extraOptionsRaw = mkOption {
        description = "Unescaped arguments to podman";
        type = listOf str;
        default = [ ];
      };

      secrets = mkOption {
        description = ''
          List of <host path>:<container path> of secrets to mount

          Instead of the host path (which must be absolute),
          the name of an Aquaris-managed secret can also be given.
        '';
        type = coercedTo
          (listOf str)
          (map (x:
            let parts = splitString ":" x; in
            assert builtins.length parts == 2; rec {
              host = pipe parts [
                (flip builtins.elemAt 0)
                (x: if hasPrefix "/" x then x else secret x)
              ];

              cont = builtins.elemAt parts 1;

              name = builtins.hashString "sha256" host;
            }))
          (listOf (submodule {
            options = {
              host = mkOption { type = path; };
              cont = mkOption { type = path; };
              name = mkOption { type = str; };
            };
          }));
        default = [ ];
      };
    };

    config = {
      environment = {
        SSL_CERT_FILE = "${pkgs.cacert}/etc/ssl/certs/ca-bundle.crt";
      };

      extraOptions = [ "--hostuser" name "--tz" top.config.time.timeZone ];

      extraOptionsRaw = (flip map config.secrets)
        (x: ''-v "''${CREDENTIALS_DIRECTORY}/${x.name}:${x.cont}:ro"'');
    };
  };

  deps = name: cfg: pipe { inherit (cfg) cmd environment; } [
    builtins.toJSON
    (pkgs.writeText "${name}-info")
    (info: (pkgs.runCommand "${name}-volumes" {
      __structuredAttrs = true;
      exportReferencesGraph.graph = info;
      nativeBuildInputs = with pkgs; [ jq ];
    }) ''
      jq -r '
        .graph
        | map(.path)
        | sort
        | .[]
      ' "$NIX_ATTRS_JSON_FILE" \
      | grep -v "${info}"      \
      | sed -E 's|(.*)|-v \1:\1:ro|' > $out

      jq -r '
        .graph[]
        | select(.path == "${info}")
        | .references[]
      ' "$NIX_ATTRS_JSON_FILE"       \
      | while read -r i; do
        if test -d "$i/bin"; then
          find "$i/bin"              \
            -mindepth 1 -maxdepth 1  \
            -not -type d -executable \
          | sed -E 's|(.+)/([^/]+)$|-v \1/\2:/bin/\2:ro|'
        fi
      done >> $out
    '')
  ];

  cfg = config.virtualisation.pnoc;
in
{
  options.virtualisation.pnoc = mkOption {
    type = attrsOf (submodule container);
    default = { };
  };

  config = {
    users = pipe cfg [
      builtins.attrNames
      (map (x: {
        users.${x} = {
          group = x;
          isSystemUser = true;
        };
        groups.${x} = { };
      }))
      aquaris.lib.merge
    ];

    systemd.services = flip mapAttrs' cfg (name: cfg: {
      name = "podman-${name}";
      value = mkIf (cfg.secrets != [ ]) {
        serviceConfig = pipe cfg.secrets [
          (map (x: { LoadCredential = "${x.name}:${x.host}"; }))
          zipAttrs
        ];

        script = mkBefore ''
          ${pkgs.util-linux}/bin/mount -v -o remount,rw "''${CREDENTIALS_DIRECTORY}"

          ${join (map (x: ''
            ${pkgs.coreutils}/bin/chown -v ${name} "''${CREDENTIALS_DIRECTORY}/${x.name}"
          '') cfg.secrets)}

          ${pkgs.util-linux}/bin/mount -v -o remount,ro "''${CREDENTIALS_DIRECTORY}"
        '';
      };
    });

    virtualisation = {
      podman = {
        package = pkgs.podman // { override = _: pkgs.podman; };
        defaultNetwork.settings.dns_enabled = true;
      };

      oci-containers.containers = flip builtins.mapAttrs cfg (name: cfg: {
        inherit (cfg)
          cmd
          environment
          environmentFiles
          extraOptions
          ports
          volumes
          workdir
          ;

        image = join [
          (join cfg.extraOptionsRaw)
          "$(< ${deps name cfg})"
          "${empty.imageName}:${empty.imageTag}"
        ];

        imageFile = empty;

        user = name;
      });
    };
  };
}
